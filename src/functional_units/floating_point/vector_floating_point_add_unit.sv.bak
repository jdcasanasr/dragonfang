module vector_floating_point_add_unit

import 	dragonfang_pkg::*,
		dragonfang_floating_point_pkg::*;

(
	input 	logic 				clock,
	input 	logic 				reset_n,
	input 	execution_vector_t 	execution_vector,
	
	input 	logic [63:0] 		vs2,
	input 	logic [63:0] 		vs1,
	
	output 	logic [63:0] 		vd_low,
	output 	logic [63:0]		vd_high
);

	execution_vector_t 	execution_vector_ff_0;
	logic 	[63:0]		vs2_ff_0;
	logic 	[63:0]		vs1_ff_0;

	logic 	[63:0]		vd_low_bus;
	logic 	[63:0]		vd_high_bus;
	
	double_t 			vs2_double;
	double_t 			vs1_double;
	float_t [1:0] 		vs2_float;
	float_t [1:0] 		vs1_float;
	
	// Manage inter-stage flip-flop's.
	always_ff @ (posedge clock, negedge reset_n)
		if (!reset_n)
			begin
				execution_vector_ff_0 	<= '0;
				vs2_ff_0				<= '0;
				vs1_ff_0 				<= '0;
				
				vd_low					<= '0;
				vd_high					<= '0;
			end
			
		else
			begin
				execution_vector_ff_0 	<= execution_vector;
				vs2_ff_0				<= vs2;
				vs1_ff_0 				<= vs1;
				
				vd_low					<= vd_low_bus;
				vd_high					<= vd_high_bus;
			end
	
	// Pack bit streams into double_t and float_t structures.
	always_comb
		begin
			vs2_double 		= pack_to_double(vs2_ff_0);
			vs1_double 		= pack_to_double(vs1_ff_0);
			
			vs2_float[0] 	= pack_to_float(vs2_ff_0[31:0]);
			vs2_float[1] 	= pack_to_float(vs1_ff_0[63:32]);
			
			vs1_float[0] 	= pack_to_float(vs1_ff_0[31:0]);
			vs1_float[1] 	= pack_to_float(vs1_ff_0[63:32]);
		end
	
	// Manage sums
	always_comb
		case (execution_vector_ff_0.bit_mode)
			ENABLED_64BIT_MODE 	:
				if (execution_vector_ff_0.subtraction_mode == ENABLED_SUBTRACTION_MODE)
					begin
						vd_low_bus 	= unpack_double_to_logic(sub_double(vs2_double, vs1_double));
						vd_high_bus = '0;
					end
				
				else if (execution_vector_ff_0.reverse_subtraction_mode == ENABLED_REVERSE_SUBTRACTION_MODE)
					begin
						vd_low_bus 	= unpack_double_to_logic(sub_double(vs1_double, vs2_double));
						vd_high_bus = '0;
					end
					
				else
					begin
						vd_low_bus 	= unpack_double_to_logic(add_double(vs2_double, vs1_double));
						vd_high_bus = '0;
					end
			
			// Only the 32-bit mode allows for widening!.
			ENABLED_32BIT_MODE 	:
				if (execution_vector_ff_0.widening_mode == ENABLED_WIDENING_MODE)
					if (execution_vector_ff_0.subtraction_mode == ENABLED_SUBTRACTION_MODE)
						begin
							vd_low_bus 	= unpack_double_to_logic(float_to_double(sub_float(vs2_float[0], vs1_float[0])));
							vd_high_bus = unpack_double_to_logic(float_to_double(sub_float(vs2_float[1], vs1_float[1])));
						end
				
					else if (execution_vector_ff_0.reverse_subtraction_mode == ENABLED_REVERSE_SUBTRACTION_MODE)
						begin
							vd_low_bus 	= unpack_double_to_logic(float_to_double(sub_float(vs1_float[0], vs2_float[0])));
							vd_high_bus = unpack_double_to_logic(float_to_double(sub_float(vs1_float[1], vs2_float[1])));
						end
					
					else
						begin
							vd_low_bus 	= unpack_double_to_logic(float_to_double(add_float(vs2_float[0], vs1_float[0])));
							vd_high_bus = unpack_double_to_logic(float_to_double(add_float(vs2_float[1], vs1_float[1])));
						end
						
				else
					if (execution_vector_ff_0.subtraction_mode == ENABLED_SUBTRACTION_MODE)
						begin
							vd_low_bus[31:0] 	= unpack_float_to_logic(sub_float(vs2_float[0], vs1_float[0]));
							vd_low_bus[63:32]	= unpack_float_to_logic(sub_float(vs2_float[1], vs1_float[1]));
							vd_high_bus 		= '0;
						end
				
					else if (execution_vector_ff_0.reverse_subtraction_mode == ENABLED_REVERSE_SUBTRACTION_MODE)
						begin
							vd_low_bus[31:0] 	= unpack_float_to_logic(sub_float(vs1_float[0], vs2_float[0]));
							vd_low_bus[63:32]	= unpack_float_to_logic(sub_float(vs1_float[1], vs2_float[1]));
							vd_high_bus 		= '0;
						end
					
					else
						begin
							vd_low_bus[31:0] 	= unpack_float_to_logic(add_float(vs1_float[0], vs2_float[0]));
							vd_low_bus[63:32]	= unpack_float_to_logic(add_float(vs1_float[1], vs2_float[1]));
							vd_high_bus 		= '0;
						end
		
			default				:
				begin
					vd_low_bus 	= '0;
					vd_high_bus = '0;
				end
		endcase
	
endmodule 