module vector_floating_point_sign_injection_unit

import 	dragonfang_pkg::*,
		dragonfang_floating_point_pkg::*;

(
	input 	logic 				clock,
	input 	logic 				reset_n,
	input 	execution_vector_t 	execution_vector,
	
	input 	logic [63:0] 		vs2,
	input 	logic [63:0] 		vs1,
	
	output 	logic [63:0] 		vd_low
);

	logic [63:0] 		vs2_ff_0;
	logic [63:0] 		vs1_ff_0;
	execution_vector_t 	execution_vector_ff_0;

	logic [63:0] 		vd_low_bus;
	
	float_t [1:0] 		vs2_float;
	float_t [1:0] 		vs1_float;
	
	double_t 			vs2_double;
	double_t			vs1_double;
	
	float_t [1:0]		vd_float;
	double_t 			vd_double;

	always_ff @ (posedge clock, negedge reset_n)
		if (!reset_n)
			begin
				vs2_ff_0 				<= '0;
				vs1_ff_0 				<= '0;
				
				execution_vector_ff_0 	<= '0;
				
				vd_low					<= '0;
			end
			
		else
			begin
				vs2_ff_0 				<= vs2;
				vs1_ff_0 				<= vs1;
				
				execution_vector_ff_0 	<= execution_vector;
				
				vd_low 					<= vd_low_bus;
			end

	always_comb
		begin
			vs2_float[0] 	= pack_to_float(vs2_ff_0[31:0]);
			vs2_float[1] 	= pack_to_float(vs2_ff_0[63:32]);
			
			vs1_float[0] 	= pack_to_float(vs1_ff_0[31:0]);
			vs1_float[1] 	= pack_to_float(vs1_ff_0[63:32]);
			
			vs2_double 		= pack_to_double(vs2_ff_0);
			vs1_double 		= pack_to_double(vs1_ff_0);
		end
		
	always_comb
		begin
			vd_float[0].exponent 	= vs2_float[0].exponent;
			vd_float[0].mantissa 	= vs2_float[0].mantissa;
			
			vd_float[1].exponent 	= vs2_float[1].exponent;
			vd_float[1].mantissa	= vs2_float[1].mantissa;
			
			vd_double.exponent		= vs2_double.exponent;
			vd_double.mantissa 		= vs2_double.mantissa;
		end
		
	always_comb
		case (execution_vector_ff_0.sign_injection_mode)
			ENABLED_VS1_SIGN_INJECTION_MODE 		:
				case (execution_vector_ff_0.bit_mode)
					ENABLED_64BIT_MODE 	:
						begin
							vd_double.sign 	= vs1_double.sign;
							vd_low_bus 		= unpack_double_to_logic(vd_double);
						end
					
					ENABLED_32BIT_MODE 	:
						begin
							vd_float[0].sign 	= vs1_float[0].sign;
							vd_float[1].sign 	= vs1_float[1].sign;
							
							vd_low_bus[31:0] 	= unpack_float_to_logic(vd_float[0]);
							vd_low_bus[63:32] 	= unpack_float_to_logic(vd_float[1]);
						end
					
					default				: vd_low_bus = '0;
				endcase
			
			ENABLED_NEGATED_VS1_SIGN_INJECTION_MODE :
				case (execution_vector_ff_0.bit_mode)
					ENABLED_64BIT_MODE 	:
						begin
							vd_double.sign 	= ~vs1_double.sign;
							vd_low_bus 		= unpack_double_to_logic(vd_double);
						end
					
					ENABLED_32BIT_MODE 	:
						begin
							vd_float[0].sign 	= ~vs1_float[0].sign;
							vd_float[1].sign 	= ~vs1_float[1].sign;
							
							vd_low_bus[31:0] 	= unpack_float_to_logic(vd_float[0]);
							vd_low_bus[63:32] 	= unpack_float_to_logic(vd_float[1]);
						end
					
					default				: vd_low_bus = '0;
				endcase
			
			ENABLED_XOR_SIGN_INJECTION_MODE 		:
				case (execution_vector_ff_0.bit_mode)
					ENABLED_64BIT_MODE 	:
						begin
							vd_double.sign 	= vs2_double.sign ^ vs1_double.sign;
							vd_low_bus 		= unpack_double_to_logic(vd_double);
						end
					
					ENABLED_32BIT_MODE 	:
						begin
							vd_float[0].sign 	= vs2_float[0].sign ^ vs1_float[0].sign;
							vd_float[1].sign 	= vs2_float[1].sign ^ vs1_float[1].sign;
							
							vd_low_bus[31:0] 	= unpack_float_to_logic(vd_float[0]);
							vd_low_bus[63:32] 	= unpack_float_to_logic(vd_float[1]);
						end
					
					default				: vd_low_bus = '0;
				endcase
			
			default 								: vd_low_bus = '0;
		endcase
			
endmodule 