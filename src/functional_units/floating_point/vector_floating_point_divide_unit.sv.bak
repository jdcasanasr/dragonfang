module vector_floating_point_divide_unit

import 	dragonfang_pkg::*,
		dragonfang_floating_point_pkg::*;

(
	input 	logic 				clock,
	input 	logic 				reset_n,
	input 	execution_vector_t 	execution_vector,
	
	input 	logic [63:0] 		vs2,
	input 	logic [63:0] 		vs1,
	
	output 	logic [63:0] 		vd_low
);

	execution_vector_t execution_vector_ff_0;
	logic [63:0] vs2_ff_0;
	logic [63:0] vs1_ff_0;
	
	logic [63:0] vs2_ff_0_effective;
	logic [63:0] vs1_ff_0_effective;
	
	logic [63:0] vd_low_bus;
	logic [63:0] vd_high_bus;
	
	double_t vs2_double;
	double_t vs1_double;
	
	float_t [1:0] vs2_float;
	float_t [1:0] vs1_float;
	
	// Manage inter-stage flip-flop's.
	always_ff @ (posedge clock, negedge reset_n)
		if (!reset_n)
			begin
				execution_vector_ff_0 	<= '0;
				vs2_ff_0 				<= '0;
				vs1_ff_0 				<= '0;
				
				vd_low					<= '0;
			end
			
		else
			begin
				execution_vector_ff_0 	<= execution_vector;
				vs2_ff_0 				<= vs2;
				vs1_ff_0 				<= vs1;
				
				vd_low					<= vd_low_bus;
			end
	
	always_comb
		if (execution_vector_ff_0.reverse_mode == ENABLED_REVERSE_MODE)
			begin
				vs2_ff_0_effective = vs1_ff_0;
				vs1_ff_0_effective = vs2_ff_0;
			end
					
		else
			begin
				vs2_ff_0_effective = vs2_ff_0;
				vs1_ff_0_effective = vs1_ff_0;
			end
			
	always_comb
		begin
			vs2_double 		= pack_to_double(vs2_ff_0_effective);
			vs1_double 		= pack_to_double(vs1_ff_0_effective);
			
			vs2_float[0] 	= pack_to_float(vs2_ff_0_effective[31:0]);
			vs2_float[1] 	= pack_to_float(vs2_ff_0_effective[63:32]);
			
			vs1_float[0] 	= pack_to_float(vs1_ff_0_effective[31:0]);
			vs1_float[1] 	= pack_to_float(vs1_ff_0_effective[63:32]);
		end
			
	always_comb
		// Return only an inverted (1 / vs2) version of vs2.
		if (execution_vector_ff_0.reciprocal_mode == ENABLED_RECIPROCAL_MODE)
			case (execution_vector_ff_0.bit_mode)
				ENABLED_64BIT_MODE 	:
					vd_low_bus 				= unpack_double_to_logic(invert_double(vs2_double));

				
				ENABLED_32BIT_MODE 	:
					begin
						vd_low_bus[31:0] 	= unpack_float_to_logic(invert_float(vs2_float[0]));
						vd_low_bus[63:32] 	= unpack_float_to_logic(invert_float(vs2_float[1]));
					end
				
				default				:
					vd_low_bus 	= '0;
					
			endcase
		
		else
			// Perform common division.
			case (execution_vector_ff_0.bit_mode)
				ENABLED_64BIT_MODE 	:
					vd_low_bus 				= unpack_double_to_logic(divide_double(vs2_double, vs1_double));

				
				ENABLED_32BIT_MODE 	:
					begin
						vd_low_bus[31:0] 	= unpack_float_to_logic(divide_float(vs2_float[0], vs1_float[0]));
						vd_low_bus[63:32] 	= unpack_float_to_logic(divide_float(vs2_float[1], vs1_float[1]));
					end
				
				default				:
					vd_low_bus 	= '0;
					
			endcase
	
endmodule 